/*
 * Copyright 2014 WisePersist.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'maven'
//    apply plugin: 'idea'
apply plugin: 'signing'

group = 'org.wisepersist'
archivesBaseName = 'wise-persist'
version = '1.0.3'

sourceCompatibility = 1.7
targetCompatibility = 1.7

apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/pmd.gradle"
apply from: "$rootDir/gradle/findbugs.gradle"

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        // fail eagerly on version conflict (includes transitive dependencies)
        // e.g. multiple different versions of the same dependency (group and name are equal)
        failOnVersionConflict()

        // force certain versions of dependencies (including transitive)
        //  *append new forced modules:
        force 'xml-apis:xml-apis:2.0.2',
              'org.slf4j:slf4j-api:1.7.6'
    }
}

test {
    // enable TestNG support (default is JUnit)
    useTestNG()
}

dependencies {
    // DI
    compile 'com.google.inject:guice:3.0'

    // Persist
    compile 'org.eclipse.persistence:javax.persistence:2.1.0'
    compile 'org.eclipse.persistence:org.eclipse.persistence.jpa:2.5.2'

    // Utilities
    compile 'com.google.guava:guava:17.0'

    // Logging
    compile 'org.slf4j:slf4j-api:1.7.6'

    // test dependencies
    testCompile 'ch.qos.logback:logback-core:1.0.13'
    testCompile 'ch.qos.logback:logback-classic:1.0.13'

    /*
      This is required by logback confitional processing, for more info please
      see: http://logback.qos.ch/setup.html#janino

      The org.codehaus.janino:commons-compiler:2.6.1 dependency will be
      automatically pulled in by Maven's transitivity rules
    */
    testCompile 'org.codehaus.janino:janino:2.6.1'

    testCompile 'com.h2database:h2:1.4.178'
    testCompile 'org.jmockit:jmockit:1.8'
    testCompile 'org.testng:testng:6.8.8'
}

build {
    doFirst {
        delete 'out'
    }
}

ext.userHome = System.getProperty("user.home")

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'WisePersist'
                packaging 'jar'
                description 'WisePersist is a simple JPA wrapper which provides @Transactional and @NonTransactional annotations and pre-configured Guice module.'
                url 'http://wisepersist.org/'

                scm {
                    connection 'scm:git:git@github.com:jiakuan/wise-persist.git'
                    developerConnection 'scm:git:git@github.com:jiakuan/wise-persist.git'
                    url 'git@github.com:jiakuan/wise-persist.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jiakuan'
                        name 'Jiakuan Wang'
                        email 'jiakuan.wang@gmail.com'
                        organization 'Headwise Systems'
                        organizationUrl 'http://headwise.systems'
                        roles {
                            role 'architect'
                            role 'developer'
                        }
                        timezone '+8'
                    }
                }
            }
        }
    }
}

//uploadArchives {
//    repositories {
//        mavenDeployer {
//            repository(url: "file://${userHome}/Dropbox/Public/mvn-repo/")
//        }
//    }
//}

task createPom << {
    pom {
        project {
            packaging 'jar'
            build {
                plugins {
                    plugin {
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-compiler-plugin'
                        version '3.1'
                        configuration {
                            source '1.7'
                            target '1.7'
                            encoding 'UTF-8'
                        }
                    }
                }
            }
        }
    }.writeTo("$projectDir/pom.xml")
}
