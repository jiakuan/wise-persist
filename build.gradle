/*
 * Copyright 2014 WiseTime Team.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'maven'
//    apply plugin: 'idea'

group = 'io.wisetime'
version = '1.0.1-SNAPSHOT'
archivesBaseName = 'wise-persist'

sourceCompatibility = 1.7
targetCompatibility = 1.7

apply from: "$rootDir/checkstyle.gradle"
apply from: "$rootDir/pmd.gradle"
apply from: "$rootDir/findbugs.gradle"

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        // fail eagerly on version conflict (includes transitive dependencies)
        // e.g. multiple different versions of the same dependency (group and name are equal)
        failOnVersionConflict()

        // force certain versions of dependencies (including transitive)
        //  *append new forced modules:
        force 'xml-apis:xml-apis:2.0.2'
    }
}

dependencies {
    // DI
    compile 'com.google.inject:guice:3.0'

    // Persist
    compile 'org.eclipse.persistence:javax.persistence:2.1.0'

    // Utilities
    compile 'com.google.guava:guava:15.0'

    // Logging
    compile 'org.slf4j:slf4j-api:1.7.5'
}

build {
    doFirst {
        delete 'out'
    }
}

ext.userHome = System.getProperty("user.home")
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${userHome}/mvn-repo/")
        }
    }
}

task createPom << {
    pom {
        project {
            packaging 'jar'
            build {
                plugins {
                    plugin {
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-compiler-plugin'
                        version '3.1'
                        configuration {
                            source '1.7'
                            target '1.7'
                            encoding 'UTF-8'
                        }
                    }
                }
            }
        }
    }.writeTo("$projectDir/pom.xml")
}
